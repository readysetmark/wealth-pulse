
(* Transaction *)

transaction = header <newline> entries
entries = (entry-or-note <newline>)+
<entry-or-note> = <ws> entry | <ws> note | <ws>


(* Transaction Entry *)

entry = account [<ws> amount] [<ws> value] [<ws> note] [<ws>]

account = #"[^ ;\t\r\n]+"

(* kind of irrelevant and untested since I don't use these currently *)
value = total-cost | unit-cost
total-cost = "@@" <ws> amount
unit-cost = "@" <ws> amount

amount = commodity <ws?> quantity | quantity <ws?> commodity | quantity
quantity = #"-?[\d,.]+"
commodity = quoted | unquoted
<quoted> = <"\""> #"[^\r\n\\\"]+" <"\"">
<unquoted> = #"[^-0123456789., @;\\r\\n\\\"]+"


(* Transaction Header *)

header = date <ws> status <ws> optional-code-then-payee [note]
<optional-code-then-payee> = code <ws> payee | !code payee

date = #"\d{4}-\d{2}-\d{2}"
status = #"[*!]"
code = <"("> #"[^);\r\n]+" <")">
payee = #"[^;\r\n]+"
note = <";"> #"[^\r\n]*"


(* Whitespace *)

ws = #"[ \t]+"

(* Newline *)

newline = "\n" / "\r\n" / "\r"
